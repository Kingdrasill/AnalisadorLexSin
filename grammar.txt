PROGRAMA -> LISTAFUNCOES PRINCIPAL
LISTAFUNCOES -> DECFUNCAO LISTAFUNCOES | ε
DECFUNCAO -> TIPORETORNO id ( PARAMETROS ) BLOCO
TIPORETORNO -> TIPO | void
TIPO -> TIPOBASE DIMENSAO
TIPOBASE -> char | float | int | boolean
DIMENSAO -> [ num_int ] DIMENSAO | ε
PARAMETROS -> TIPO id LISTAPARAMETROS | ε
LISTAPARAMETROS -> , TIPO id LISTAPARAMETROS | ε
PRINCIPAL -> main ( ) BLOCO
BLOCO -> { LISTAVARIAVEIS COMANDOS }
LISTAVARIAVEIS -> TIPO id LISTAID ; LISTAVARIAVEIS | ε
LISTAID -> , id LISTAID | ε
COMANDOS -> COMANDO COMANDOS | ε
COMANDO -> LEITURA | ESCRITA | ATRIBUICAO | FUNCAO | SELECAO | ENQUANTO | RETORNO
LEITURA -> scanf ( TERMOLEITURA NOVOTERMOLEITURA ) ;
TERMOLEITURA -> id DIMENSAO2
NOVOTERMOLEITURA -> , TERMOLEITURA NOVOTERMOLEITURA | ε
DIMENSAO2 -> [ EXPR_ADITIVA ] DIMENSAO2 | ε
ESCRITA -> println ( TERMOESCRITA NOVOTERMOESCRITA ) ;
TERMOESCRITA -> id DIMENSAO2 | CONSTANTE | texto
NOVOTERMOESCRITA -> , TERMOESCRITA NOVOTERMOESCRITA | ε
SELECAO -> if ( EXPRESSAO ) BLOCO SENAO
SENAO -> else BLOCO | ε
ENQUANTO -> while ( EXPRESSAO ) BLOCO
ATRIBUICAO -> id = COMPLEMENTO ;
COMPLEMENTO -> EXPRESSAO | FUNCAO
FUNCAO -> func id ( ARGUMENTOS )
ARGUMENTOS -> EXPRESSAO NOVO_ARGUMENTO | ε
NOVO_ARGUMENTO -> , EXPRESSAO NOVO_ARGUMENTO | ε
RETORNO -> return EXPRESSAO ;
EXPRESSAO -> EXPR_OU
EXPR_OU -> EXPR_E EXPR_OU2
EXPR_OU2 -> || EXPR_E EXPR_OU2 | ε
EXPR_E -> EXPR_RELACIONAL EXPR_E2
EXPR_E2 -> && EXPR_RELACIONAL EXPR_E2 | ε
EXPR_RELACIONAL -> EXPR_ADITIVA EXPR_RELACIONAL2
EXPR_RELACIONAL2 -> comp EXPR_ADITIVA | ε
EXPR_ADITIVA -> EXPR_MULTIPLICATIVA EXPR_ADITIVA2
EXPR_ADITIVA2 -> OP_ADITIVO EXPR_MULTIPLICATIVA EXPR_ADITIVA2 | ε
OP_ADITIVO -> + | -
EXPR_MULTIPLICATIVA -> FATOR EXPR_MULTIPLICATIVA2
EXPR_MULTIPLICATIVA2 -> OP_MULTIPLICATIVO FATOR EXPR_MULTIPLICATIVA2 | ε
OP_MULTIPLICATIVO -> * | / | %
FATOR -> SINAL TERMO | texto | ! FATOR | ( EXPRESSAO )
TERMO -> id DIMENSAO2 | CONSTANTE
SINAL -> + | - | ε
CONSTANTE -> num_int | num_dec